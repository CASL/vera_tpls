# This will configure and build hypre
# User can configure the source path by speficfying HYPRE_SRC_DIR

IF ( NOT HYPRE_SRC_DIR )
    MESSAGE(FATAL_ERROR "Please specify HYPRE_SRC_DIR")
ENDIF()

MESSAGE("   HYPRE_SRC_DIR = ${HYPRE_SRC_DIR}")
FILE(MAKE_DIRECTORY "${CMAKE_INSTALL_PREFIX}/hypre")

SET( HYPRE_BUILD_DIR "${CMAKE_BINARY_DIR}/HYPRE-prefix/src/HYPRE-build" )
SET( COPY_HYPRE_SRC 1 )     # Copy src to a temporary directory, or build in src tree

SET( CONFIGURE_OPTIONS --with-blas=yes --with-lapack=yes )
SET( CONFIGURE_OPTIONS ${CONFIGURE_OPTIONS} --with-blas-lib="${BLAS_LIBS}" )
SET( CONFIGURE_OPTIONS ${CONFIGURE_OPTIONS} --with-blas-lib-dirs="${BLAS_DIR}" )
SET( CONFIGURE_OPTIONS ${CONFIGURE_OPTIONS} --with-lapack-libs="${LAPACK_LIBS}" )
SET( CONFIGURE_OPTIONS ${CONFIGURE_OPTIONS} --with-lapack-lib-dirs="${LAPACK_DIR}" )
IF ( ${CMAKE_BUILD_TYPE} STREQUAL "Debug" )
    SET( CONFIGURE_OPTIONS ${CONFIGURE_OPTIONS} --enable-debug )
ELSEIF ( ${CMAKE_BUILD_TYPE} STREQUAL "Release" )
    SET( CONFIGURE_OPTIONS ${CONFIGURE_OPTIONS} --enable-optimize )
ELSE()
    MESSAGE ( FATAL_ERROR "Unknown CMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}" )
ENDIF()
IF ( ENABLE_SHARED )
    SET( CONFIGURE_OPTIONS ${CONFIGURE_OPTIONS} --enable-shared )
ELSE()
    SET( CONFIGURE_OPTIONS ${CONFIGURE_OPTIONS} --disable-shared )
ENDIF()
IF ( ENABLE_STATIC )
    SET( CONFIGURE_OPTIONS ${CONFIGURE_OPTIONS} --enable-static )
ELSE()
    SET( CONFIGURE_OPTIONS ${CONFIGURE_OPTIONS} --disable-static )
ENDIF()

IF ( COPY_HYPRE_SRC )
    SET( HYPRE_CMAKE_URL            "${HYPRE_SRC_DIR}"   )
    SET( HYPRE_CMAKE_DOWNLOAD_DIR   "${HYPRE_BUILD_DIR}" )
    SET( HYPRE_CMAKE_SOURCE_DIR     "${HYPRE_BUILD_DIR}" )
ELSE()
    SET( HYPRE_CMAKE_URL            ""   )
    SET( HYPRE_CMAKE_DOWNLOAD_DIR   "" )
    SET( HYPRE_CMAKE_SOURCE_DIR     "${HYPRE_SRC_DIR}" )
ENDIF()

EXTERNALPROJECT_ADD(
    HYPRE
    URL                 "${HYPRE_CMAKE_URL}"
    DOWNLOAD_DIR        "${HYPRE_CMAKE_DOWNLOAD_DIR}"
    SOURCE_DIR          "${HYPRE_CMAKE_SOURCE_DIR}"
    UPDATE_COMMAND      ""
    CONFIGURE_COMMAND   ./configure --prefix=${CMAKE_INSTALL_PREFIX}/hypre ${CONFIGURE_OPTIONS} ${ENV_VARS}
    BUILD_COMMAND       make -j ${PROCS_INSTALL}
    BUILD_IN_SOURCE     1
    INSTALL_COMMAND     make install
    DEPENDS             LAPACK
)



